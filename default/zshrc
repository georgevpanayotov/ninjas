unsetopt beep
setopt inc_append_history

bindkey -v

# By default, in vi mode, backspace only deletes characters that have been typed since entering
# insert mode. This allows backspace to delete without constraint.
bindkey -M viins "^?" backward-delete-char

alias jerbs=jobs
alias exeunt=exit
alias dirs=dirs\ -p

() {
    local platZsh="$(uname).zsh"
    (which $platZsh > /dev/null 2> /dev/null) && source $platZsh
}

MODE_PROMPT='$'

# Update the prompt string to include the "prompt_header"
# and set the screen's title if we're inside of a screen.
update_dir() {
    local PROMPT_HEADER="$(prompt_header.py)"
    local INCOGNITO_PROMPT=""
    if (( $INCOGNITO )); then
        INCOGNITO_PROMPT="[i]"
    fi
    PS1=" ##$INCOGNITO_PROMPT $PROMPT_HEADER%c $MODE_PROMPT "
    if [[ "$STY" != "" ]]; then
        stitle.sh
    fi
}

chpwd() {
    # Update the prompt and screen title whenever we change pwd.
    update_dir
}

zshaddhistory() {
    if [[ "$1" == $'incognito\n' ]]; then
        return 1
    fi

    if (( ! INCOGNITO )); then
        echo -n $1 >> ~/.zshpermanenthistory
    else
        return 1
    fi
}

pvim() {
    vim $@ -c "setlocal buftype=nofile" -c "setlocal bufhidden=hide" -c "setlocal noswapfile" -
}

svim() {
    vim $@ -c "setlocal buftype=nofile" -c "setlocal bufhidden=hide" -c "setlocal noswapfile"
}

() {
    if [[ "$SUPPRESS_SCREEN" != "" ]]; then
        return
    fi

    if [[ "$SCREEN_CMD" == "" ]]; then
        local SCREEN_CMD=screen
    fi

    if [[ $(screen -ls | grep -i ${USER}_scr | grep -v "Dead ?" | wc -l) -eq 0 ]]; then
        $SCREEN_CMD -d -m -S ${USER}_scr
    fi

    if [[ $(screen -ls | grep "Dead ?" | wc -l) -ne 0 && "$STY" == "" ]]; then
        echo "Dead screen detected."
        screen -ls | grep "Dead ?"
    fi
}

# Inserts the parameter into the buffer at the current cursor's location. Moves the cursor to the
# end of the inserted token.
insert_in_buffer() {
  local selected=$1

  if [[ "$selected" != "" ]]; then
    local pre=$BUFFER[1,$CURSOR]
    local post=$BUFFER[$(($CURSOR + 1)),$#BUFFER]
    if [[ "$pre[$#pre]" != " " ]]; then
      pre="$pre "
    fi
    if [[ "$post" != "" && "$post[1]" != " " ]]; then
      post=" $post"
    fi

    BUFFER="$pre$selected$post"
    CURSOR=$(( $#pre + $#selected ))
  fi
}

incognito() {
    INCOGNITO=1
    update_dir
}

uncognito() {
    INCOGNITO=0
    update_dir
}

# Update the prompt and screen title at startup.
update_dir

# Attempts to set a valid agent SSH_AUTH_SOCK if the current one is empty or invalid. This is useful
# when remoting into a screen session and using agent forwarding. This has limitations. It won't
# work if the screen session uses a valid socket that just happens to not include necessary
# credentials. In this case, you must manually delete the socket and then run this. If this happens,
# you may want to restore the old socket so that subsequent local uses of the screen session will
# still work.
update_ssh_agent() {
    if [[ "$SSH_AUTH_SOCK" != "" && -e "$SSH_AUTH_SOCK" ]]; then
        # Already have a valid agent.
        return
    fi

    local -a agents
    local i
    local choice

    agents=($(find /tmp/ -path /tmp/ssh-\*/\* -name agent.\* 2> /dev/null))

    if [[ $#agents -eq 0 ]]; then
        echo "No potential agents found."
    elif [[ $#agents -eq 1 ]]; then
        export SSH_AUTH_SOCK=$agents[1]
    else
        for i in {1..$#agents}; do
            echo "$i: ${agents[$i]}"
        done
        read choice
        if [[ $choice -gt 0 && $choice -le $#agents ]]; then
            export SSH_AUTH_SOCK=$agents[$choice]
        else
            echo "Invalid choice"
        fi

    fi

}

function zle-line-init zle-keymap-select  {
    if [[ $KEYMAP == "vicmd" ]]; then
        MODE_PROMPT=":"
    elif [[ $KEYMAP == "main" ||$KEYMAP == "viins"  ]]; then
        MODE_PROMPT="$"
    fi
    update_dir
    zle reset-prompt
}

zle -N zle-line-init
zle -N zle-keymap-select

[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh
